import faylib::Font, Vec2, Color
import rectangle::Rectangle
import entity_system::EntitySystem
import sprite::Textures
import user_interface::UserInterface

enum Scene {
	MainMenu { start: bool }
	DifficultySelector {
		one: bool
		two: bool
		three: bool
	}
	InGame { difficulty: isize, shield_health: i32 }
	Lost { reason: str, ok: bool }
	Won { ok: bool }
}

method mut Scene
fn transitioned(context: *mut Context) {
	match self {
		MainMenu => self.transitioned(context)
		DifficultySelector => self.transitioned(context)
		InGame => self.transitioned(context)
		Lost => self.transitioned(context)
		Won => self.transitioned(context)
	}
}

method mut Scene
fn tick(context: *mut Context) {
	match self {
		MainMenu => self.tick(context)
		DifficultySelector => self.tick(context)
		InGame => self.tick(context)
		Lost => self.tick(context)
		Won => self.tick(context)
	}

	context.user_interface.tick(context)
	context.entity_system.tick(context)
}

struct Context {
	textures: *Textures
	entity_system: *mut EntitySystem
	user_interface: *mut UserInterface
	font: Font
	scene: *mut Scene
	viewport_size: Vec2
	delta: f32
}

method mut Context
fn transition(to: Scene) {
	self.entity_system.clear()
	self.user_interface.clear()
	self.scene.* = to
	self.scene.transitioned(self)
}

method mut Scene::MainMenu
fn transitioned(context: *mut Context) {
	context.user_interface.heading = "Fae Invaders"
	context.user_interface.push(.Button {
		text: "Start"
		pressed: self.start.&mut
	})
}

method mut Scene::MainMenu
fn tick(context: *mut Context) {
	if self.start {
		context.transition(.DifficultySelector {
			one: false
			two: false
			three: false
		})
	}
}

method mut Scene::DifficultySelector
fn transitioned(context: *mut Context) {
	context.user_interface.heading = "Select Difficulty"
	context.user_interface.push(.Button {
		text: "Easy"
		pressed: self.one.&mut
	})
	context.user_interface.push(.Button {
		text: "Normal"
		pressed: self.two.&mut
	})
	context.user_interface.push(.Button {
		text: "Hard"
		pressed: self.three.&mut
	})
	context.user_interface.selection = 1
}

method mut Scene::DifficultySelector
fn tick(context: *mut Context) {
	let shield_health: i32 = 3
	if self.one => context.transition(.InGame { difficulty: 1, shield_health })
	else if self.two => context.transition(.InGame { difficulty: 2, shield_health })
	else if self.three => context.transition(.InGame { difficulty: 3, shield_health })
}

method mut Scene::InGame
fn transitioned(context: *mut Context) {
	import entities::player::Player
	import entities::enemy::Enemy, Direction

	for y_offset in 0..self.difficulty * 2 {
		let direction: Direction = if y_offset % 2 == 0  => yield Direction.Left
		else => yield Direction.Right
		let y_offset = y_offset.(f32) * context.textures.enemy.height.(f32) * 1.6

		for x_offset in -5..6 {
			let x_offset = x_offset.(f32) * context.textures.enemy.width.(f32) * 1.55
			let position = Vec2 {
				x: context.viewport_size.x / 2 + x_offset
				y: context.textures.enemy.height.(f32) + y_offset
			}
			Enemy.spawn(context.textures, context.entity_system, position, direction)
		}
	}
	
	Player.spawn(context.textures, context.entity_system, context.viewport_size)
}

method mut Scene::InGame
fn tick(context: *mut Context) {
	const ShieldHeight = 30

	if context.entity_system.player.all_dead() {
		let reason = "You Died!"
		context.transition(.Lost { reason, ok: false })
		return
	}

	if context.entity_system.enemy.all_dead() {
		context.transition(.Won { ok: false })
		return
	}

	for offset in 1..self.shield_health.(isize) + 1 {
		let height = offset.(f32) * ShieldHeight
		let rectangle = Rectangle {
			x: 0
			y: context.viewport_size.y - height
			width: context.viewport_size.x
			height
		}

		let color = Color.rgba(50, 50, 255, 50)
		faylib::draw_rectangle(rectangle, color)
	}
}

method mut Scene::Lost
fn transitioned(context: *mut Context) {
	context.user_interface.heading = self.reason
	context.user_interface.push(.Button {
		text: "Continue"
		pressed: self.ok.&mut
	})
}

method mut Scene::Lost
fn tick(context: *mut Context) {
	if self.ok => context.transition(.MainMenu { start: false })
}

method mut Scene::Won
fn transitioned(context: *mut Context) {
	context.user_interface.heading = "You Won!"
	context.user_interface.push(.Button {
		text: "Continue"
		pressed: self.ok.&mut
	})
}

method mut Scene::Won
fn tick(context: *mut Context) {
	if self.ok => context.transition(.MainMenu { start: false })
}
