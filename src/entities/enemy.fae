import faylib.Vec2
import sprite.Sprite, Textures
import entity_system.EntitySystem, Context
import entities.bomb.Bomb

const MaxDirectionDistance = 30 // Pixels
const Speed = 40 // Pixels per second
const InitialFireDelay = 0.2 // Seconds
const FireDelay = 1 // Seconds

struct Enemy {
	sprite: Sprite
	direction: Direction
	distance_traveled: f32
	max_distance: f32
	fire_delay: f32
}

enum Direction {
	Left
	Right
}

method static Enemy
fn spawn(textures=: *Textures, entity_system=: *mut EntitySystem, position: Vec2, direction: Direction) {
	entity_system.enemy.push(Enemy {
		sprite: Sprite {
			texture: textures.enemy.&
			position
		}
		direction
		distance_traveled: 0
		max_distance: MaxDirectionDistance
		fire_delay: choose_initial_fire_delay()
	})
}

fn choose_initial_fire_delay(): f32 {
	let random = faylib.get_random_value(0, 10).(f32)
	return InitialFireDelay + random / 10
}

fn choose_fire_delay(): f32 {
	let random = faylib.get_random_value(0, 20).(f32)
	return FireDelay + random / 6
}

method mut Enemy
fn tick(context: *mut Context<Enemy>) {
	mut remaining_distance = Speed * context.delta

	while remaining_distance > 0 {
		let allowed_distance = self.max_distance - (self.distance_traveled + remaining_distance)
		let distance = if allowed_distance <= 0 or remaining_distance < allowed_distance {
			yield remaining_distance
		} else => yield allowed_distance
		remaining_distance -= distance
		self.distance_traveled += distance

		match self.direction {
			Left {
				self.sprite.position.x -= distance
				if self.distance_traveled >= self.max_distance {
					self.direction = Direction.Right
					self.distance_traveled = 0
					self.max_distance = MaxDirectionDistance * 2
				}
			}

			Right {
				self.sprite.position.x += distance
				if self.distance_traveled >= self.max_distance {
					self.direction = Direction.Left
					self.distance_traveled = 0
					self.max_distance = MaxDirectionDistance * 2
				}
			}
		}
	}

	self.fire_delay -= context.delta
	if self.fire_delay <= 0 {
		self.fire_delay = choose_fire_delay()

		let remaining = context.entity_system.enemy.live_count
		let initial = context.scene.initial_enemy_count
		let percent_remaining = remaining.(f64) / initial.(f64)

		let remaining_boost = (1 - percent_remaining) * 0.4
		const BaseChance = 0.08
		let chance = (BaseChance + remaining_boost).min(1)

		let upper = 1 / chance
		let rounded_upper = upper.round().(i32).max(1)

		if faylib.get_random_value(1, rounded_upper) == 1 {
			Bomb.fire(context.textures, context.sounds, context.entity_system, position: self.sprite.position)
		}
	}

	self.sprite.draw()
}
