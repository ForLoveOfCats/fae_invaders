import faylib::Vec2
import sprite::Sprite, Textures
import entity_system::EntitySystem, Context
import entities::missile::Missile

struct Player {
	sprite: Sprite
	velocity: Vec2
	fire_delay: f32
}

method static Player
fn spawn(textures: *Textures, entity_system: *mut EntitySystem, viewport_size: Vec2) {
	entity_system.player.push(Player {
		sprite: Sprite {
			texture: textures.player.&
			position: Vec2 {
				x: viewport_size.x / 2
				y: viewport_size.y - textures.player.height.(f32)
			}
		}
		velocity: Vec2.zero()
		fire_delay: 0
	})
}

method mut Player
fn tick(context: *mut Context<Player>) {
	import faylib::is_key_down

	const TopSpeed = 500 // Pixels per second
	const Acceleration = 800 // Pixels per second per second
	const Drag = 2800 // Pixels per second per second
	const FireDelay = 0.145 // Seconds

	let drag = Drag * context.delta
	let acceleration = Acceleration * context.delta

	if is_key_down(key::A) => self.velocity.x -= acceleration + drag
	if is_key_down(key::D) => self.velocity.x += acceleration + drag

	if self.velocity.x < 0 => self.velocity.x = (self.velocity.x + drag).min(0)
	if self.velocity.x > 0 => self.velocity.x = (self.velocity.x - drag).max(0)

	if self.velocity.x < -TopSpeed => self.velocity.x = -TopSpeed
	if self.velocity.x > TopSpeed => self.velocity.x = TopSpeed

	let distance = Vec2 { x: self.velocity.x * context.delta, y: 0 }
	self.sprite.move(context.viewport_size, distance)

	self.fire_delay -= context.delta
	if self.fire_delay <= 0 and user_interface::is_fire_pressed() {
		let half_height = self.sprite.texture.height.(f32) / 2
		let random = faylib::get_random_value(-8, 8).(f32)
		let position = Vec2 {
			x: self.sprite.position.x + random
			y: self.sprite.position.y - half_height
		}
		Missile.fire(context.textures, context.sounds, context.entity_system, position)
		self.fire_delay = FireDelay
	}

	self.sprite.draw()
}
