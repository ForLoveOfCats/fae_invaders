import faylib::Color, Vec2
import sprite::Textures
import entity_system::EntitySystem
import entities::player::Player
import entities::enemy::Enemy

fn set_up_window(): Vec2 {
	const WindowWidth = 2600
	const WindowHeight = 2000

	faylib::set_trace_log_level(faylib::LogWarning)
	faylib::init_window(WindowWidth, WindowHeight, "Fae Raylib".pointer)

	let monitor = faylib::get_current_monitor()
	let refresh_rate = faylib::get_monitor_refresh_rate(monitor)
	faylib::set_target_fps(refresh_rate)
	println(f"Set refresh rate of {refresh_rate}hz")

	return Vec2 { x: WindowWidth, y: WindowHeight }
}

fn main() {
	let window_size = set_up_window()

	let textures = Textures.load()
	defer textures.free()

	mut entity_system = EntitySystem.new()
	defer entity_system.free()

	Player.spawn(textures.&, entity_system.&mut, window_size)
	Enemy.spawn(textures.&, entity_system.&mut, window_size)

	while faylib::window_should_close().! {
		faylib::begin_drawing()
		defer faylib::end_drawing()

		faylib::clear_background(Color.white())

		entity_system.tick(textures.&, window_size, faylib::get_frame_time())
	}
}
